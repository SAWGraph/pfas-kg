# Use Case 1: Testing Gaps
# CQ 2 (low complexity)
# What surface water bodies are near (same or neighboring S2 cell) landfills or DoD sites?
# Run this from the Hydrology repository

PREFIX fio: <http://sawgraph.spatialai.org/v1/fio#>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX hyf: <https://www.opengis.net/def/schema/hy_features/hyf/>
PREFIX kwg-ont: <http://stko-kwg.geog.ucsb.edu/lod/ontology/>
PREFIX naics: <http://sawgraph.spatialai.org/v1/fio/naics#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX us_frs: <http://sawgraph.spatialai.org/v1/us-frs#>

SELECT * WHERE {
    # Find NHD water bodies and the S2 cells that overlap them
    #    Note: S2 cells within a waterbody are ignored since landfills or DoD sites
    #          should not be in those S2 cells (within a waterbody)
    ?wb rdf:type hyf:HY_WaterBody ;
        rdfs:label ?wblabel ;
        geo:hasGeometry/geo:asWKT ?wb_wkt .
    ?wb_s2 kwg-ont:sfOverlaps ?wb .

    SERVICE <repository:S2L13_AdminRegions> {
        SELECT * WHERE {
            # Find S2 cells that neighbor each S2 cell that contains a landfill or DoD site
            #    and is a neighbor of an S2 cell that overlaps an NHD waterbody
            ?fac_s2 kwg-ont:sfTouches ?wb_s2 .
            OPTIONAL { ?fac kwg-ont:sfWithin ?wb_s2 . }
            ?fac_s2 geo:hasGeometry/geo:asWKT ?fac_s2_wkt .
            ?fac_s2_nbrs kwg-ont:sfTouches ?fac_s2 ;
                         geo:hasGeometry/geo:asWKT ?fac_s2_nbrs_wkt .
        }
    }

    SERVICE <repository:FIO> {
        {
            SELECT * WHERE {
                # Find landfills and their containing S2 cells
                ?fac rdf:type fio:Facility ;
                     fio:ofIndustry naics:NAICS-Industry-Code-562212 ;
                     fio:ofIndustry ?code ;
                     kwg-ont:sfWithin ?fac_s2 ;
                     geo:hasGeometry/geo:asWKT ?fac_wkt .
                # If available, find additional information for each landfill
                OPTIONAL { ?fac rdfs:label ?faclabel . }
                OPTIONAL { ?code rdfs:label ?ind . }
        	  }
        }
        UNION
        {
             SELECT * WHERE {
                # Find DoD sites and their containing S2 cells
                ?fac rdf:type us_frs:Federal-Facility ;
               	  	 us_frs:primaryIndustry ?code ;
                	 kwg-ont:sfWithin ?fac_s2 ;
                     geo:hasGeometry/geo:asWKT ?fac_wkt.
                FILTER (?code IN (naics:NAICS-Industry-Code-92811, naics:NAICS-Industry-Code-928110))
                # If available, find additional information for each DoD site
                OPTIONAL { ?fac rdfs:label ?faclabel . }
                OPTIONAL { ?code rdfs:label ?ind . }
            }
   		  }
    }
}
