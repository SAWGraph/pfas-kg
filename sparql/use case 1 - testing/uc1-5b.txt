# Use Case 1: Testing Gaps
# CQ 5b (High Complexity), an extension of CQ 5
# What wells are hydrologically connected to PFOA contaminated private water supply wells where results have been above 200 ng/L? It additionally counts how many values over 200 have been observed in the aquifer a well is connected to in order to prioritize future testing.

# Run this from the SAWGraph repository

PREFIX me_mgs: <http://sawgraph.spatialai.org/v1/me-mgs#>
PREFIX me_mgs-data: <http://sawgraph.spatialai.org/v1/me-mgs-data#>
PREFIX coso: <http://sawgraph.spatialai.org/v1/contaminoso#>
PREFIX gwml: <http://www.opengis.net/gwml-main/2.2/>
PREFIX kwg-ont: <http://stko-kwg.geog.ucsb.edu/lod/ontology/>
PREFIX me_egad: <http://sawgraph.spatialai.org/v1/me-egad#>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX unit: <http://qudt.org/vocab/unit>

SELECT DISTINCT ?well (MAX (?num_val) AS ?max_value) (COUNT (?num_val) AS ?counts) WHERE { 
    # Find EGAD sample points and restrict them to private water supply wells
    ?samp_pt rdf:type me_egad:EGAD-SamplePoint .
    ?sp_type rdfs:label 'PRIVATE WATER SUPPLY WELL' .
    # Find PFOA observations and results for each EGAD sample point
	?obs coso:observedAtSamplePoint ?samp_pt ;
    	 coso:ofSubstance me_egad:parameter.PFOA_A ;
    	 sosa:hasResult ?result .
    # Find PFOA levels for each result, limiting them to units of ng/L.
    ?result qudt:quantityValue ?quant_val .
    ?quant_val qudt:numericValue ?num_val ;
    		   qudt:unit unit:NanoGM-PER-L .
    # Keep only results over 200 ng/L PFOA
    FILTER(?num_val > 200)
    
    # Find the S2 cell each private water supply well is within
    ?samp_pt kwg-ont:sfWithin ?sp_s2 .
    
    SERVICE <repository:Hydrology> {
        SELECT * WHERE {
            # Find aquifers that are coincident with the private water supply wells using the S2 cells
            ?sp_s2 kwg-ont:sfWithin | kwg-ont:sfOverlaps ?aq .
            ?aq rdf:type gwml:GW_Aquifer .
            # Find all S2 cells that are within or overlap an aquifer
            ?aq_s2 kwg-ont:sfWithin | kwg-ont:sfOverlaps ?aq .
        }
    }
    
    # Find all wells that access the aquifers
    ?well rdf:type me_mgs:MGS-Well ;
                me_mgs:hasUse me_mgs-data:d.wellUse.Domestic.
    ?well kwg-ont:sfWithin ?aq_s2 .
} GROUP BY ?well ORDER BY DESC(?counts) DESC(?max_value)