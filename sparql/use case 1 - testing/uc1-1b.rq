# Use Case 1: Testing Gaps
# CQ 1b
# In Penobscot county, what wells are near (in same or adjacent S2 cell as) as landfills (as facilities of concern)?

### GraphDB Version ###
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX kwg-ont: <http://stko-kwg.geog.ucsb.edu/lod/ontology/>
PREFIX kwgr: <http://stko-kwg.geog.ucsb.edu/lod/resource/>
PREFIX me_mgs: <http://sawgraph.spatialai.org/v1/me-mgs#>
PREFIX fio: <http://w3id.org/fio/v1/fio#>
PREFIX naics: <http://w3id.org/fio/v1/naics#>
PREFIX epa_frs: <http://w3id.org/fio/v1/epa-frs#>

SELECT DISTINCT ?facility ?well ?use WHERE {
    SERVICE <repository:FIO> {
    	?facility rdf:type epa_frs:EPA-PFAS-Facility ;
    		  fio:ofIndustry naics:NAICS-562212 .
        ?facility kwg-ont:sfWithin | kwg-ont:sfOverlaps ?s2cell .
    }

    SERVICE <repository:Hydrology>
    {
        ?well a me_mgs:MGS-Well .
        ?well me_mgs:hasUse ?use .
        ?well me_mgs:ofWellType ?wellType .
        ?well kwg-ont:sfWithin ?s2neighbor .
    }

    SERVICE <repository:Spatial> {
	?s2cell rdf:type kwg-ont:S2Cell_Level13 .
        ?s2cell kwg-ont:sfTouches ?s2neighbor .
        ?s2neighbor kwg-ont:sfWithin | kwg-ont:sfOverlaps kwgr:administrativeRegion.USA.23019 .
    }
}


### FRINK Version ###
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX kwg-ont: <http://stko-kwg.geog.ucsb.edu/lod/ontology/>
PREFIX kwgr: <http://stko-kwg.geog.ucsb.edu/lod/resource/>
PREFIX me_mgs: <http://sawgraph.spatialai.org/v1/me-mgs#>
PREFIX fio: <http://w3id.org/fio/v1/fio#>
PREFIX naics: <http://w3id.org/fio/v1/naics#>
PREFIX epa_frs: <http://w3id.org/fio/v1/epa-frs#>

SELECT DISTINCT ?facility ?well ?use WHERE {
  ?facility rdf:type epa_frs:EPA-PFAS-Facility ;
            fio:ofIndustry naics:NAICS-562212 .
  ?facility kwg-ont:sfWithin | kwg-ont:sfOverlaps ?s2cell .
  ?well a me_mgs:MGS-Well .
  ?well me_mgs:hasUse ?use .
  ?well me_mgs:ofWellType ?wellType .
  ?well kwg-ont:sfWithin ?s2neighbor .
  ?s2cell rdf:type kwg-ont:S2Cell_Level13 .
  ?s2cell kwg-ont:sfTouches ?s2neighbor .
  ?s2neighbor kwg-ont:sfWithin | kwg-ont:sfOverlaps kwgr:administrativeRegion.USA.23019 .
} 
