# Use Case 1: Testing Gaps
# CQ 1
# What wells are near (in same s2 cell as) sources (facilities of concern)?
# This query is currently limited to Maine because it uses the me_mgs namespace

PREFIX kwg-ont: <http://stko-kwg.geog.ucsb.edu/lod/ontology/>
PREFIX me_mgs: <http://sawgraph.spatialai.org/v1/me-mgs#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX us_frs: <http://sawgraph.spatialai.org/v1/us-frs#>

SELECT ?well ?wellUse ?wellType ?facilityName ?facility WHERE {
    SERVICE <repository:FIO> 
    {
        ?facility rdf:type us_frs:EPA-PFAS-Facility ;
        		  rdfs:label ?facilityName .
        ?s2 kwg-ont:sfContains ?facility .
    }   

    SERVICE <repository:Spatial>
    {
        ?s2 kwg-ont:cellID ?s2ID .
    }
    
    SERVICE <repository:Hydrology>
    {
        ?well a me_mgs:MGS-Well ;
              me_mgs:hasUse ?wellUse ;
              me_mgs:ofWellType ?wellType ;
              kwg-ont:sfWithin ?s2 .
    }
    
} ORDER BY ?facilityName ?wellUse
